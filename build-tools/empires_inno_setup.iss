; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "RaiseTheEmpires"
#define MyAppVersion "0.08a"
#define MyAppPublisher "RaiseTheEmpires"
#define MyAppURL "https://github.com/AcidCaos/raisetheempires"
#define MyAppExeName "empires-server.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{19162E79-2BB2-48A6-866B-822AC47CEB0D}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; The [Icons] "quicklaunchicon" entry uses {userappdata} but its [Tasks] entry has a proper IsAdminInstallMode Check.
UsedUserAreasWarning=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=lowest
; License file
LicenseFile=C:\empires\build-tools\License.txt
;PrivilegesRequiredOverridesAllowed=dialog
OutputDir=C:\empires-installer
OutputBaseFilename=empires-setup
SetupIconFile=C:\empires\build-tools\icon.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "armenian"; MessagesFile: "compiler:Languages\Armenian.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
Name: "corsican"; MessagesFile: "compiler:Languages\Corsican.isl"
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"
Name: "finnish"; MessagesFile: "compiler:Languages\Finnish.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"
Name: "icelandic"; MessagesFile: "compiler:Languages\Icelandic.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "norwegian"; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "slovenian"; MessagesFile: "compiler:Languages\Slovenian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

[Types]
Name: "full"; Description: "Full installation"
Name: "compact"; Description: "Minimum installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "required"; Description: "Empires and Allies main files"; Types: full compact custom; Flags: fixed
Name: "chromium"; Description: "Chromium 82.0 (required if you don't have any Flash-enabled browser)"; Types: full
Name: "savespack"; Description: "Titan savegame (if you want to try using Titan) and bonus starter-pack world. Please backup your savegame first, it will replace your save!!"; Types: full 

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 6.1; Check: not IsAdminInstallMode

[Files]
;x64
Source: "C:\empires\redist\vc_redist.x64.exe"; DestDir: "{tmp}"; Components: required; Check: Is64BitInstallMode; Flags: deleteafterinstall
Source: "C:\dist_mini008_x64\empires-server.exe"; DestDir: "{app}"; Components: required; Check: Is64BitInstallMode; Flags: ignoreversion
Source: "C:\empires\chromium-x64\*"; DestDir: "{app}\chromium"; Components: chromium; Check: Is64BitInstallMode; Flags: ignoreversion recursesubdirs createallsubdirs
;x86
Source: "C:\empires\\redist\vc_redist.x86.exe"; DestDir: "{tmp}"; Components: required; Check: not Is64BitInstallMode; Flags: solidbreak deleteafterinstall
Source: "C:\dist_mini008_x86\empires-server.exe"; DestDir: "{app}"; Components: required; Check: not Is64BitInstallMode; Flags: ignoreversion
Source: "C:\empires\chromium-x86\*"; DestDir: "{app}\chromium"; Components: chromium; Check: not Is64BitInstallMode; Flags: ignoreversion recursesubdirs createallsubdirs
;common
Source: "C:\empires\build-tools\master_preferences"; DestDir: "{app}\chromium"; Components: chromium; Flags: solidbreak ignoreversion
Source: "C:\empires\redist\flashplayer32_0r0_371_winpep.exe"; DestDir: "{tmp}"; Components: chromium; Flags: deleteafterinstall
Source: "C:\empires\allies\*"; DestDir: "{userdocs}\My Games\{#MyAppName}\allies"; Components: required; Flags: solidbreak ignoreversion recursesubdirs createallsubdirs
Source: "C:\empires\mods\*"; DestDir: "{userdocs}\My Games\{#MyAppName}\mods"; Components: required; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\empires\mods\README.MD"; DestDir: "{app}\mods"; Components: required; Flags: solidbreak ignoreversion recursesubdirs createallsubdirs
Source: "C:\empires\assets\*"; DestDir: "{app}\assets"; Components: required; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\empires\templates\*"; DestDir: "{app}\templates"; Components: required; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\empires\gamesettings-converted.json"; DestDir: "{userdocs}\My Games\{#MyAppName}"; Components: required; Flags: ignoreversion
Source: "C:\empires\questsettings-converted.json"; DestDir: "{userdocs}\My Games\{#MyAppName}"; Components: required; Flags: ignoreversion
Source: "C:\empires\savespack\save.db"; DestDir: "{userdocs}\My Games\{#MyAppName}"; Components: savespack; Flags: solidbreak uninsneveruninstall
Source: "C:\empires\save.db"; DestDir: "{userdocs}\My Games\{#MyAppName}"; Components: required; Flags: solidbreak onlyifdoesntexist uninsneveruninstall
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[INI]
Filename: "{app}\RaiseTheEmpires.ini"; Section: "Info"; Flags: uninsdeletesection
Filename: "{app}\RaiseTheEmpires.ini"; Section: "Info"; Key: "Name"; String: "{#MyAppName}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "Info"; Key: "Version"; String: "{#MyAppVersion}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "Info"; Key: "Publisher"; String: "{#MyAppPublisher}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "Info"; Key: "URL"; String: "{#MyAppURL}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "Info"; Key: "Binary"; String: "{#MyAppExeName}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "InstallFolders"; Flags: uninsdeletesection
Filename: "{app}\RaiseTheEmpires.ini"; Section: "InstallFolders"; Key: "InstallPath"; String: "{app}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "InstallFolders"; Key: "MyGamesPath"; String: "{userdocs}\My Games\{#MyAppName}"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "InstallSettings"; Flags: uninsdeletesection
Filename: "{app}\RaiseTheEmpires.ini"; Section: "InstallSettings"; Check: Is64BitInstallMode; Key: "Arch"; String: "x64"
Filename: "{app}\RaiseTheEmpires.ini"; Section: "InstallSettings"; Check: not Is64BitInstallMode; Key: "Arch"; String: "x86"

[UninstallDelete]
Type: files; Name: "{app}\RaiseTheEmpires.ini"
Type: dirifempty; Name: "{app}"
Type: filesandordirs; Name: "{userdocs}\My Games\{#MyAppName}\chromium-profile"
Type: filesandordirs; Name: "{userdocs}\My Games\{#MyAppName}\cache"

[Run]
Filename: "{tmp}\flashplayer32_0r0_371_winpep.exe"; Components: chromium; Flags: shellexec; StatusMsg: Installing Adobe Flash Player 32.0.0.371 PPAPI...
Filename: "{tmp}\vc_redist.x64.exe"; Check: Is64BitInstallMode and VCRedistNeedsInstall; StatusMsg: Installing Visual Studio Runtime x64 Libraries...
Filename: "{tmp}\vc_redist.x86.exe"; Check: (not Is64BitInstallMode) and VCRedist86NeedsInstall; StatusMsg: Installing Visual Studio Runtime x86 Libraries...
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
#IFDEF UNICODE
  #DEFINE AW "W"
#ELSE
  #DEFINE AW "A"
#ENDIF
type
  INSTALLSTATE = Longint;

  const
  INSTALLSTATE_INVALIDARG = -2;  { An invalid parameter was passed to the function. }
  INSTALLSTATE_UNKNOWN = -1;     { The product is neither advertised or installed. }
  INSTALLSTATE_ADVERTISED = 1;   { The product is advertised but not installed. }
  INSTALLSTATE_ABSENT = 2;       { The product is installed for a different user. }
  INSTALLSTATE_DEFAULT = 5;      { The product is installed for the current user. }

  { Visual C++ 2017 Redistributable 14.16.27024 }
  VC_2017_REDIST_X84_ADD = '{7258184A-EC44-4B1A-A7D3-68D85A35BFD0}';
  VC_2017_REDIST_X84_MIN = '{5EEFCEFB-E5F7-4C82-99A5-813F04AA4FBD}';

  VC_2017_REDIST_X64_ADD = '{9D29FC96-9EEE-4253-943F-96B3BBFDD0B6}';
  VC_2017_REDIST_X64_MIN = '{F1B0FB3A-E0EA-47A6-9383-3650655403B0}';

  { Visual C++ 2015 Redistributable 14.0.23026 }
  VC_2015_REDIST_X86_MIN = '{A2563E55-3BEC-3828-8D67-E5E8B9E8B675}';
  VC_2015_REDIST_X64_MIN = '{0D3E9E15-DE7A-300B-96F1-B4AF12B96488}';

  VC_2015_REDIST_X86_ADD = '{BE960C1C-7BAD-3DE6-8B1A-2616FE532845}';
  VC_2015_REDIST_X64_ADD = '{BC958BD2-5DAC-3862-BB1A-C1BE0790438D}';

function MsiQueryProductState(szProduct: string): INSTALLSTATE;
  external 'MsiQueryProductState{#AW}@msi.dll stdcall';

function VCVersionInstalled(const ProductID: string): Boolean;
begin
  Result := MsiQueryProductState(ProductID) = INSTALLSTATE_DEFAULT;
end;

function VCRedistNeedsInstall: Boolean;
begin
  Result := not VCVersionInstalled(VC_2015_REDIST_X64_MIN);
end;

function VCRedist86NeedsInstall: Boolean;
begin
  Result := not VCVersionInstalled(VC_2015_REDIST_X86_MIN);
end;
